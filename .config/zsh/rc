#!/usr/bin/env zsh

. $XDG_CONFIG_HOME/shell/aliases
. $XDG_CONFIG_HOME/zsh/opts

autoload -Uz colors && colors

zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "$XDG_CACHE_DIR/zsh/zcompcache"
zstyle ':completion:*' auto-description '%d'
zstyle ':completion:*' completer _expand _complete _ignored _match _correct \
                       _approximate _prefix
zstyle ':completion:*' expand suffix
zstyle ':completion:*' group-name ''
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list \
  'm:{[:lower:]}={[:upper:]}' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' \
  'r:|[._-/]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' menu select=1
zstyle ':completion:*' original false
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' select-prompt \
  %SScrolling active: current selection at %p%s
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' use-compctl true
zstyle ':completion:*' verbose true

autoload -U url-quote-magic
zle -N self-insert url-quote-magic

autoload -Uz compinit
compinit -d "$XDG_CACHE_HOME/zsh/zcompdump"

REPORTTIME=5

# Cache this, because it's quite slow.
HOMEBREW_PREFIX="$(brew --prefix)"
HOMEBREW_REPO="$(brew --repo)"

load_from_brew() {
  . "$HOMEBREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
  . "$HOMEBREW_PREFIX/share/zsh-history-substring-search/zsh-history-substring-search.zsh"
  . "$HOMEBREW_PREFIX/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
}

load_from_brew || (brew bundle --file="$ZDOTDIR/Brewfile" && load_from_brew)

zmodload zsh/complist
bindkey -M menuselect "^[[Z" reverse-menu-complete

zmodload zsh/terminfo
bindkey "^[[A" history-substring-search-up
bindkey "^[[B" history-substring-search-down

bindkey "^[[3~" delete-char
bindkey "^[[F" end-of-line
bindkey "^[[H" beginning-of-line
bindkey "^[[1~" beginning-of-line
bindkey "^[[4~" end-of-line
bindkey "^[^[[C" forward-word
bindkey "^[^[[D" backward-word

autoload -U edit-command-line
zle -N edit-command-line
bindkey "^x^e" edit-command-line

if [[ -v TMUX ]]; then
  _tmux_hook() {
    local exit_status="$?"
    tmux set-environment "last_exit_status_$(tmux display-message -p '#D')" "$exit_status"
    tmux refresh-client -S
  }

  typeset -ag precmd_functions;
  if [[ -z ${precmd_functions[(r)_tmux_hook]} ]]; then
    precmd_functions+=_tmux_hook;
  fi
fi

if [ "$(uname -s)" = "Darwin" ]
then
  # Load SSH passphrases from the Keychain.
  # Use an explicit path because Homebrew OpenSSH
  # doesn't have the keychain functionality.
  (/usr/bin/ssh-add -A &) 2> /dev/null
fi

homebrew_taps_dir="$HOMEBREW_REPO/Library/Taps"
command_not_found_repo="$homebrew_taps_dir/homebrew/homebrew-command-not-found"
if [ -d "$command_not_found_repo" ]; then
  . "$command_not_found_repo/handler.sh"
fi

if command -v direnv > /dev/null; then
  eval "$(direnv hook zsh)"
fi

if [ -d "$HOMEBREW_PREFIX/opt/z" ]; then
  source "$HOMEBREW_PREFIX/opt/z/etc/profile.d/z.sh"
fi

if [ -d "$HOMEBREW_PREFIX/opt/fzf" ]; then
  source "$HOMEBREW_PREFIX/opt/fzf/shell/key-bindings.zsh"
fi
