#!/usr/bin/env zsh

. $XDG_CONFIG_HOME/shell/aliases
. $XDG_CONFIG_HOME/zsh/opts

autoload -Uz colors && colors

zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path "$XDG_CACHE_DIR/zsh/zcompcache"
zstyle ':completion:*' auto-description '%d'
zstyle ':completion:*' completer _expand _complete _ignored _match _correct \
                       _approximate _prefix
zstyle ':completion:*' expand suffix
zstyle ':completion:*' group-name ''
zstyle ':completion:*' insert-unambiguous true
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-suffixes true
zstyle ':completion:*' matcher-list \
  'm:{[:lower:]}={[:upper:]}' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' \
  'r:|[._-/]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' menu select=1
zstyle ':completion:*' original false
zstyle ':completion:*' preserve-prefix '//[^/]##/'
zstyle ':completion:*' select-prompt \
  %SScrolling active: current selection at %p%s
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' use-compctl true
zstyle ':completion:*' verbose true

autoload -U url-quote-magic
zle -N self-insert url-quote-magic

# compaudit doesn't take into account that I might want to manage completions
# through a trusted group.
autoload -Uz compinit
local compaudit_dirs=$(compaudit 2>&1 | tail -n +2)
local ok_pattern='^........-. \d+ root brew'
if [ -n "$compaudit_dirs" ] && ls -ld $compaudit_dirs | ag -v $ok_pattern; then
  echo "compaudit detected insecure directories."
else
  compinit -u -d "$XDG_CACHE_HOME/zsh/zcompdump"
fi

REPORTTIME=5

insert_sudo () { zle beginning-of-line; zle -U "sudo " }
zle -N insert-sudo insert_sudo
bindkey "^[s" insert-sudo
bindkey -e

# Cache this, because it's quite slow.
HOMEBREW_PREFIX=$(brew --prefix)

load_from_brew() {
  . "$HOMEBREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
  . "$HOMEBREW_PREFIX/opt/zsh-history-substring-search/zsh-history-substring-search.zsh"
  . "$HOMEBREW_PREFIX/opt/liquidprompt/share/liquidprompt"
}

load_from_brew || (brew bundle --file="$ZDOTDIR/Brewfile" && load_from_brew)

zmodload zsh/terminfo
bindkey "^[[A" history-substring-search-up
bindkey "^[[B" history-substring-search-down

# Fix delete key
bindkey "^[[3~" delete-char

if [ "$(uname -s)" = "Darwin" ]
then
  # Load SSH passphrases from the Keychain.
  # Use an explicit path because Homebrew OpenSSH
  # doesn't have the keychain functionality.
  (/usr/bin/ssh-add -K &) 2> /dev/null
fi

eval "$(brew command-not-found-init)"

source "$HOMEBREW_PREFIX/opt/chruby/share/chruby/chruby.sh"
source "$HOMEBREW_PREFIX/opt/chruby/share/chruby/auto.sh"
chruby "$(< "$XDG_CONFIG_HOME/chruby/version")"
